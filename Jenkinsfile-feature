import groovy.json.JsonOutput
import groovy.json.JsonSlurper
import java.util.regex.Pattern

/*
 * Sends a rocket chat notification
 */
def notifyRocketChat(text, url) {
    def rocketChatURL = url
    def message = text.replaceAll(~/\'/, "")
    def payload = JsonOutput.toJson([
      "username":"Jenkins",
      "icon_url":"https://wiki.jenkins.io/download/attachments/2916393/headshot.png",
      "text": message
    ])

    sh("curl -X POST -H 'Content-Type: application/json' --data \'${payload}\' ${rocketChatURL}")
}

// Print stack trace of error
@NonCPS
private static String stackTraceAsString(Throwable t) {
    StringWriter sw = new StringWriter();
    t.printStackTrace(new PrintWriter(sw));
    return sw.toString()
}

def _openshift(String name, String project, Closure body) {
  script {
    openshift.withCluster() {
      openshift.withProject(project) {
        echo "Running Stage '${name}'"
        waitUntil {
          boolean isDone=false
          try {
            body()
            isDone=true
            echo "Completed Stage '${name}'"
          } catch (error) {
            echo "${stackTraceAsString(error)}"
            def inputAction = input(
              message: "This step (${name}) has failed. See related messages:",
              ok: 'Confirm',
              parameters: [
                choice(
                  name: 'action',
                  choices: 'Re-run\nIgnore',
                  description: 'What would you like to do?'
                )
              ]
            )
            if ('Ignore'.equalsIgnoreCase(inputAction)) {
              isDone=true
            }
          }
          return isDone
        }
      }
    }
  }
}

/*
 * takes in a sonarqube status json payload
 * and returns the status string
 */
def sonarGetStatus (jsonPayload) {
  def jsonSlurper = new JsonSlurper()
  return jsonSlurper.parseText(jsonPayload).projectStatus.status
}

/*
 * takes in a sonarqube status json payload
 * and returns the date string
 */
def sonarGetDate (jsonPayload) {
  def jsonSlurper = new JsonSlurper()
  return jsonSlurper.parseText(jsonPayload).projectStatus.periods[0].date
}

boolean sonarqubeReportComplete ( String oldDate, String sonarqubeStatusUrl, def iterations = 6 ) {
  def oldSonarqubeReportDate = oldDate
  def newSonarqubeReportDate = sonarGetDate ( sh ( returnStdout: true, script: "curl -w '%{http_code}' '${sonarqubeStatusUrl}'" ) )
  int delay = 0

  for (int i=0; i<iterations; i++) {
    echo "waiting for sonarqube report, iterator is: ${i}, max iterator is: ${iterations} \n Old Date: ${oldSonarqubeReportDate} \n New Date: ${newSonarqubeReportDate}"
    if (oldSonarqubeReportDate != newSonarqubeReportDate) {
      echo "sonarqube report complete"
      return true
    } else {
      delay = (1<<i) // exponential backoff
      sleep(delay)
      newSonarqubeReportDate = sonarGetDate ( sh ( returnStdout: true, script: "curl -w '%{http_code}' '${sonarqubeStatusUrl}'" ) )
    }
  }
  return false
}

/*
 * Updates the global pastBuilds array: it will iterate recursively
 * and add all the builds prior to the current one that had a result
 * different than 'SUCCESS'.
 */
def buildsSinceLastSuccess(previousBuild, build) {
  if ((build != null) && (build.result != 'SUCCESS')) {
    pastBuilds.add(build)
    buildsSinceLastSuccess(pastBuilds, build.getPreviousBuild())
  }
}

/*
 * Generates a string containing all the commit messages from
 * the builds in pastBuilds.
 */
@NonCPS
def getChangeLog(pastBuilds) {
  def log = ""
  for (int x = 0; x < pastBuilds.size(); x++) {
    for (int i = 0; i < pastBuilds[x].changeSets.size(); i++) {
      def entries = pastBuilds[x].changeSets[i].items
      for (int j = 0; j < entries.length; j++) {
        def entry = entries[j]
        log += "* ${entry.msg} by ${entry.author} \n"
      }
    }
  }
  return log;
}

def nodejsTester () {
  _openshift(env.STAGE_NAME, TOOLSPROJECT) {
    String testPodLabel = "node-tester-${UUID.randomUUID().toString()}";
    podTemplate(
      label: testPodLabel,
      name: testPodLabel,
      serviceAccount: 'jenkins',
      cloud: 'openshift',
      slaveConnectTimeout: 300,
      containers: [
        containerTemplate(
          name: 'jnlp',
          image: 'docker-registry.default.svc:5000/esm/eagle-unit-tester',
          resourceRequestCpu: '500m',
          resourceLimitCpu: '1000m',
          resourceRequestMemory: '2Gi',
          resourceLimitMemory: '3Gi',
          workingDir: '/tmp',
          command: '',
        )
      ]
    ) {
      node(testPodLabel) {
        checkout scm
        try {
          sh 'npm i'
          sh 'npm run tests-ci'
        } finally {
          echo "Unit Tests Passed"
        }
      }
    }
    return true
  }
}


def nodejsSonarqube () {
  _openshift(env.STAGE_NAME, TOOLSPROJECT) {
    String sonarLabel = "sonarqube-runner-${UUID.randomUUID().toString()}";
    podTemplate(
      label: sonarLabel,
      name: sonarLabel,
      serviceAccount: 'jenkins',
      cloud: 'openshift',
      slaveConnectTimeout: 300,
      containers: [
        containerTemplate(
          name: 'jnlp',
          image: 'registry.access.redhat.com/openshift3/jenkins-agent-nodejs-8-rhel7',
          resourceRequestCpu: '500m',
          resourceLimitCpu: '1000m',
          resourceRequestMemory: '2Gi',
          resourceLimitMemory: '3Gi',
          workingDir: '/tmp',
          command: '',
          args: '${computer.jnlpmac} ${computer.name}',
        )
      ]
    ) {
      node(sonarLabel) {
        checkout scm
        dir('sonar-runner') {
          try {
            // run scan
            sh("oc extract secret/sonarqube-secrets --to=${env.WORKSPACE}/sonar-runner --confirm")
            SONARQUBE_URL = sh(returnStdout: true, script: 'cat sonarqube-route-url')

            sh "npm install typescript"
            sh returnStdout: true, script: "./gradlew sonarqube -Dsonar.host.url=${SONARQUBE_URL} -Dsonar.projectKey='org.sonarqube:cac-eagle-public' -Dsonar.projectName=cac-eagle-public -Dsonar.verbose=true --stacktrace --info"

            // check if sonarqube passed
            sh("oc extract secret/sonarqube-status-urls --to=${env.WORKSPACE}/sonar-runner --confirm")
            def SONARQUBE_STATUS_URL = "${SONARQUBE_URL}/api/qualitygates/project_status?projectKey=org.sonarqube:cac-eagle-public"

            boolean firstScan = false;
            def OLD_SONAR_DATE
            try {
              // get old sonar report date
              def OLD_SONAR_DATE_JSON = sh(returnStdout: true, script: "curl -w '%{http_code}' '${SONARQUBE_STATUS_URL}'")
              OLD_SONAR_DATE = sonarGetDate (OLD_SONAR_DATE_JSON)
            } catch (error) {
              firstScan = true
            }

            if ( !firstScan ) {
              // wiat for report to be updated
              if ( !sonarqubeReportComplete ( OLD_SONAR_DATE, SONARQUBE_STATUS_URL ) ) {
                echo "sonarqube report failed to complete, or timed out"

                notifyRocketChat(
                  "@all The latest build, ${env.BUILD_DISPLAY_NAME} of cac-eagle-public seems to be broken. \n ${env.RUN_DISPLAY_URL}\n Error: \n sonarqube report failed to complete, or timed out : ${SONARQUBE_URL}",
                  ROCKET_DEPLOY_WEBHOOK
                )

                currentBuild.result = "FAILURE"
                exit 1
              }
            } else {
              sleep (30)
            }

            SONARQUBE_STATUS_JSON = sh(returnStdout: true, script: "curl -w '%{http_code}' '${SONARQUBE_STATUS_URL}'")
            SONARQUBE_STATUS = sonarGetStatus (SONARQUBE_STATUS_JSON)

            // check if sonarqube passed
            sh("oc extract secret/sonarqube-status-urls --to=${env.WORKSPACE}/sonar-runner --confirm")
            SONARQUBE_STATUS_URL = sh(returnStdout: true, script: 'cat sonarqube-status-public')

            notifyRocketChat(
              "@all The latest build ${env.BUILD_DISPLAY_NAME} of cac-eagle-public seems to be broken. \n ${env.RUN_DISPLAY_URL}\n Error: \n Sonarqube scan failed",
              ROCKET_DEPLOY_WEBHOOK
            )

            if ( "${SONARQUBE_STATUS}" == "ERROR") {
              echo "Scan Failed"

              notifyRocketChat(
                "@all The latest build ${env.BUILD_DISPLAY_NAME} of cac-eagle-public seems to be broken. \n ${env.RUN_DISPLAY_URL}\n Error: \n ${error.message}",
                ROCKET_DEPLOY_WEBHOOK
              )

              currentBuild.result = 'FAILURE'
              exit 1
            } else {
              echo "Scan Passed"
            }
          } catch (error) {
            notifyRocketChat(
              "@all The latest build ${env.BUILD_DISPLAY_NAME} of cac-eagle-public seems to be broken. \n ${env.BUILD_URL}\n Error: \n ${error.message}",
              ROCKET_DEPLOY_WEBHOOK
            )
            throw error
          } finally {
            echo "Scan Complete"
          }
        }
      }
    }
    return true
  }
}


def CHANGELOG = "No new changes"
def IMAGE_HASH = "latest"
def lockName = "cac-eagle-public-${env.JOB_NAME}-${env.BUILD_NUMBER}"
pipeline {
  environment {
    TOOLSPROJECT = "esm"
  }
  agent any
  stages {
    stage('Build Init') {
      steps {
        script {
          openshift.setLockName(lockName)
        }
      }
    }
    stage('Parallel Stage') {
      failFast true
      parallel {
        stage('Build') {
          agent any
          steps {
            script {
              pastBuilds = []
              buildsSinceLastSuccess(pastBuilds, currentBuild);
              CHANGELOG = getChangeLog(pastBuilds);

              echo ">>>>>>Changelog: \n ${CHANGELOG}"

              try {
                sh("oc extract secret/rocket-chat-secrets --to=${env.WORKSPACE} --confirm")
                ROCKET_DEPLOY_WEBHOOK = sh(returnStdout: true, script: 'cat rocket-deploy-webhook')
                ROCKET_QA_WEBHOOK = sh(returnStdout: true, script: 'cat rocket-qa-webhook')

                echo "Building eagle-public develop branch"
                openshiftBuild bldCfg: 'cac-eagle-public-angular-builder', showBuildLogs: 'true'
                openshiftBuild bldCfg: 'cac-eagle-public-build', showBuildLogs: 'true'
                echo "Build done"

                echo ">>> Get Image Hash"
                // Don't tag with BUILD_ID so the pruner can do it's job; it won't delete tagged images.
                // Tag the images for deployment based on the image's hash
                IMAGE_HASH = sh (
                  script: """oc get istag cac-eagle-public:latest -o template --template=\"{{.image.dockerImageReference}}\"|awk -F \":\" \'{print \$3}\'""",
                  returnStdout: true).trim()
                echo ">> IMAGE_HASH: ${IMAGE_HASH}"
              } catch (error) {
                notifyRocketChat(
                  "@all The build ${env.BUILD_DISPLAY_NAME} of cac-eagle-public, seems to be broken.\n ${env.RUN_DISPLAY_URL}\n Error: \n ${error.message}",
                  ROCKET_DEPLOY_WEBHOOK
                )
                throw error
              }
            }
          }
        }

        stage('Unit Test') {
          steps {
            script {
              echo "Running linter and unit tests"
              def result = nodejsTester()
            }
          }
        }

        stage('Sonarqube') {
          steps {
            script {
              echo "Running Sonarqube"
              def result = nodejsSonarqube()
            }
          }
        }
      }
    }

    stage('Deploy to dev'){
      agent any
      steps {
        script {
          try {
            echo "Deploying to dev..."
            openshiftTag destStream: 'cac-eagle-public', verbose: 'false', destTag: 'cac', srcStream: 'cac-eagle-public', srcTag: "${IMAGE_HASH}"
            sleep 5
            // todo update namespace before switching over
            openshiftVerifyDeployment depCfg: 'cac-eagle-public', namespace: 'esm-dev', replicaCount: 1, verbose: 'false', verifyReplicaCount: 'false', waitTime: 600000
            echo ">>>> Deployment Complete"

            notifyRocketChat(
              "A new version of cac-eagle-public is now in Dev, build: ${env.BUILD_DISPLAY_NAME} \n Changes: \n ${CHANGELOG}",
              ROCKET_DEPLOY_WEBHOOK
            )
            notifyRocketChat(
              "@all A new version of cac-eagle-public is now in Dev and ready for QA. \n Changes to Dev: \n ${CHANGELOG}",
              ROCKET_QA_WEBHOOK
            )
          } catch (error) {
            notifyRocketChat(
              "@all The build ${env.BUILD_DISPLAY_NAME} of cac-eagle-public, seems to be broken.\n ${env.RUN_DISPLAY_URL}\n Error: \n ${error.message}",
              ROCKET_DEPLOY_WEBHOOK
            )
            currentBuild.result = "FAILURE"
            throw new Exception("Deploy failed")
          }
        }
      }
    }

    // stage('BDD Tests') {
    //   agent { label: bddPodLabel }
      // steps{
      //   echo "BDD placeholder"
      //   echo "Build: ${BUILD_ID}"
        // checkout scm
      // }
    // }
  }
}
